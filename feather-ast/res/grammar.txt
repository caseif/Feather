OptNewline      := (Newline)
                 | Epsilon

IntType         := (TypeInt8)
                 | (TypeInt16)
                 | (TypeInt32)
                 | (TypeInt64)

UintType        := (TypeUint8)
                 | (TypeUint16)
                 | (TypeUint32)
                 | (TypeUint64)

FloatType       := (TypeFloat8)
                 | (TypeFloat16)
                 | (TypeFloat32)
                 | (TypeFloat64)

BuiltInType     := IntType
                 | UintType
                 | FloatType
                 | (TypeBool)
                 | (TypeChar)
                 | (TypeString)

ArrayModNaught  := Expression
                 | Epsilon

ArrayModifier   := (OpenBracket) ArrayModNaught (CloseBracket)

TupleTypes      := Type (Comma) TupleTypes
                 | Type

TupleType       := (OpenParen) TupleTypes (CloseParen)

Type            := BuiltInType
                 | (Identifier)
                 | Type ArrayModifier
                 | TupleType

BooleanLiteral  := (TrueLiteral)
                 | (FalseLiteral)

Literal         := (StringLiteral)
                 | (IntegerLiteral)
                 | (HexLiteral)
                 | (DecimalLiteral)
                 | (Nil)
                 | BooleanLiteral

Parenthetical   := (OpenParen) OptNewline Expression OptNewline (CloseParen)

TupleElements   := Expression (Comma) TupleElsPrime

TupleElsPrime   := Expression (Comma) TupleElsPrime
                 | Expression
                 | Epsilon

Tuple           := (OpenParen) TupleElements (CloseParen)

IndexAccess     := (OpenBracket) Expression (CloseBracket)

SliceBound      := Primary
                 | Epsilon

RangeDelimiter  := (Range)
                 | (RangeInclusive)

SliceAccess     := (OpenBracket) SliceBound RangeDelimiter SliceBound (CloseBracket)

InitListEntries := Expression (Comma) OptNewline InitListEntries
                 | Expression
                 | Epsilon

InitList        := (OpenBrace) InitListEntries (CloseBrace)

OpNumUnary      := (Hyphen)

OpBoolUnary     := (Not)

OpNumMult       := (Asterisk)
                 | (ForwardSlash)

OpNumAdd        := (Plus)
                 | (Hyphen)
                 | (Percent)

OpBoolRel       := (LessThan)
                 | (LessEqual)
                 | (GreaterThan)
                 | (GreaterEqual)

OpBoolEq        := (Equals)
                 | (NotEquals)

OpBoolAnd       := (And)

OpBoolOr        := (Or)

AddSubOperator  := (Plus)
                 | (Hyphen)

MulDivOperator  := (Asterisk)
                 | (ForwardSlash)

TypeAnnotation  := (Colon) Type

VarDecl         := (VarDeclKeyword) (Identifier) TypeAnnotation
                 | (VarDeclKeyword) (Identifier)

ConstDecl       := (ConstQualifier) (Identifier) TypeAnnotation

Lval            := VarDecl
                 | ConstDecl
                 | (Identifier)

StaticAccess    := Primary (StaticAccessor) OptNewline (Identifier)

InstanceAccess  := Primary (InstanceAccessor) OptNewline (Identifier)

Invokable       := (Identifier)
                 | StaticAccess
                 | InstanceAccess
                 | FnInvocation
                 | Parenthetical

FnParamList     := Expression FnParamListPrime
                 | Epsilon

FnParamListPrime    := (Comma) OptNewline Expression FnParamListPrime
                     | Epsilon

FnInvocation    := Invokable (OpenParen) OptNewline FnParamList OptNewline (CloseParen)

Primary         := (Identifier)
                 | StaticAccess
                 | InstanceAccess
                 | Literal
                 | Parenthetical
                 | FnInvocation
                 | Primary IndexAccess
                 | Primary SliceAccess
                 | Tuple
                 | InitList

ExprOpUnary     := OpNumUnary ExprOpUnary
                 | OpBoolUnary ExprOpUnary
                 | Primary

ExprOpNumMult   := ExprOpUnary OpNumMult OptNewline ExprOpNumMult
                 | ExprOpUnary

ExprOpNumAdd    := ExprOpNumMult OpNumAdd OptNewline ExprOpNumAdd
                 | ExprOpNumMult

ExprOpBoolRel   := ExprOpNumAdd OpBoolRel OptNewline ExprOpBoolRel
                 | ExprOpNumAdd

ExprOpBoolEq    := ExprOpBoolRel OpBoolEq OptNewline ExprOpBoolEq
                 | ExprOpBoolRel

ExprOpBoolAnd   := ExprOpBoolEq OpBoolAnd OptNewline ExprOpBoolAnd
                 | ExprOpBoolEq

ExprOpBoolOr    := ExprOpBoolOr OpBoolOr OptNewline ExprOpBoolOr
                 | ExprOpBoolAnd

Expression      := ExprOpBoolOr

Assignment      := Lval (Assign) OptNewline Expression

Block           := (OpenBrace) StatementList (CloseBrace)

IfStatement     := (If) Expression OptNewline Block

ElseIfStatement := (ElseIf) Expression OptNewline Block

ElseStatement   := (Else) OptNewline Block

IfChain         := IfStatement IfChainPrime

IfChainPrime    := ElseIfStatement IfChainPrime
                 | ElseStatement
                 | Epsilon

ImportStatement := (Import) (Identifier)

FnParamDefs     := (Identifier) TypeAnnotation FnPmDefsPrime
                 | (Identifier) FnPmDefsPrime
                 | Epsilon

FnPmDefsPrime   := (Comma) OptNewline (Identifier) TypeAnnotation FnPmDefsPrime
                 | (Comma) OptNewline (Identifier) FnPmDefsPrime
                 | Epsilon

FnReturnType    := (RightArrow) Type
                 | Epsilon

FunctionSig     := (FunctionDefKeyword) (Identifier) (OpenParen) OptNewline FnParamDefs OptNewline (CloseParen) OptNewline FnReturnType

FunctionDef     := FunctionSig OptNewline Block

ClassField      := (Identifier) TypeAnnotation (Assign) (Expression)
                 | (Identifier) TypeAnnotation

ClassFields     := ClassField (Newline) ClassFldsPrime
                 | Epsilon

ClassFldsPrime  := ClassField (Newline) ClassFldsPrime
                 | Epsilon

ClassFunctions  := FunctionDef (Newline) ClassFnsPrime
                 | Epsilon

ClassFnsPrime   := FunctionDef (Newline) ClassFnsPrime
                 | Epsilon

ClassDef        := (ClassDefKeyword) (Identifier) OptNewline (OpenBrace) (Newline) ClassFields ClassFunctions (CloseBrace)

ReturnStatement := (Return) Expression
                 | (Return)

AnnotationBody  := (Identifier)
                 | Literal

Annotation      := (AnnotationBegin) AnnotationBody (AnnotationEnd)

Annotatable     := FunctionDef

StatementEnd    := (Newline)

StatementNaught := FunctionDef
                 | ClassDef
                 | ImportStatement
                 | FnInvocation
                 | ReturnStatement
                 | Assignment
                 | VarDecl
                 | ConstDecl
                 | IfChain
                 | Block
                 | Annotation StatementEnd Annotatable

Statement       := StatementNaught StatementEnd

StatementList   := Statement StatementList
                 | StatementEnd StatementList
                 | Epsilon

Program         := StatementList

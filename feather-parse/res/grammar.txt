OptNewline      := (Newline)
                 | Epsilon

IntType         := (TypeInt8)
                 | (TypeInt16)
                 | (TypeInt32)
                 | (TypeInt64)

UintType        := (TypeUint8)
                 | (TypeUint16)
                 | (TypeUint32)
                 | (TypeUint64)

FloatType       := (TypeFloat32)
                 | (TypeFloat64)

BuiltInType     := IntType
                 | UintType
                 | FloatType
                 | (TypeBool)
                 | (TypeChar)
                 | (TypeString)

ComplexType     := (Identifier)

ArrayModNaught  := Expression
                 | Epsilon

ArrayModifier   := (OpenBracket) ArrayModNaught (CloseBracket)

ArrayType       := Type ArrayModifier

TupleTypes      := Type (Comma) TupleTypes
                 | Type

TupleType       := (OpenParen) TupleTypes (CloseParen)

Type            := BuiltInType
                 | ComplexType
                 | ArrayType
                 | TupleType

Literal         := (StringLiteral)
                 | (IntegerLiteral)
                 | (HexLiteral)
                 | (DecimalLiteral)
                 | (BooleanLiteral)
                 | (Nil)

Parenthetical   := (OpenParen) OptNewline Expression OptNewline (CloseParen)

TupleElements   := Expression (Comma) TupleElsPrime

TupleElsPrime   := Expression (Comma) TupleElsPrime
                 | Expression
                 | Epsilon

Tuple           := (OpenParen) TupleElements (CloseParen)

InitListEntries := Expression (Comma) OptNewline InitListEntries
                 | Expression
                 | Epsilon

InitList        := (OpenBrace) InitListEntries (CloseBrace)

TypeAnnotation  := (Colon) Type

VarDecl         := (VarDeclKeyword) (Identifier) TypeAnnotation
                 | (VarDeclKeyword) (Identifier)

ConstDecl       := (ConstQualifier) (Identifier) TypeAnnotation

Lval            := VarDecl
                 | ConstDecl
                 | (Identifier)

StaticAccess    := Primary (StaticAccessor) OptNewline (Identifier)

InstancedAccess  := Primary (InstanceAccessor) OptNewline (Identifier)

IndexSelector   := (OpenBracket) Expression (CloseBracket)

IndexedAccess   := Primary IndexSelector

SliceBound      := Primary
                 | Epsilon

RangeDelimiter  := (Range)
                 | (RangeInclusive)

SliceSelector   := (OpenBracket) SliceBound RangeDelimiter SliceBound (CloseBracket)

SlicedAccess    := Primary SliceSelector

Invokable       := (Identifier)
                 | StaticAccess
                 | InstancedAccess
                 | IndexedAccess
                 | FnInvocation
                 | Parenthetical

FnParamList     := Expression FnParamListPrime
                 | Epsilon

FnParamListPrime    := (Comma) OptNewline Expression FnParamListPrime
                     | Epsilon

FnInvocation    := Invokable (OpenParen) OptNewline FnParamList OptNewline (CloseParen)

OpUnary      := (Hyphen)
              | (Not)

OpNumMult       := (Asterisk)
                 | (ForwardSlash)

OpNumAdd        := (Plus)
                 | (Hyphen)
                 | (Percent)

CmpBoolRel      := (LessThan)
                 | (LessEqual)
                 | (GreaterThan)
                 | (GreaterEqual)

CmpBoolEq       := (Equals)
                 | (NotEquals)

OpBoolAnd       := (And)

OpBoolOr        := (Or)

Primary         := (Identifier)
                 | StaticAccess
                 | InstancedAccess
                 | Literal
                 | Parenthetical
                 | FnInvocation
                 | IndexedAccess
                 | SlicedAccess
                 | Tuple
                 | InitList

ExprOpUnary     := OpUnary ExprOpUnary
                 | Primary

ExprOpNumMult   := ExprOpUnary OpNumMult OptNewline ExprOpNumMult
                 | ExprOpUnary

ExprOpNumAdd    := ExprOpNumMult OpNumAdd OptNewline ExprOpNumAdd
                 | ExprOpNumMult

ExprCmpBoolRel  := ExprOpNumAdd CmpBoolRel OptNewline ExprCmpBoolRel
                 | ExprOpNumAdd

ExprCmpBoolEq   := ExprCmpBoolRel CmpBoolEq OptNewline ExprCmpBoolEq
                 | ExprCmpBoolRel

ExprOpBoolAnd   := ExprCmpBoolEq OpBoolAnd OptNewline ExprOpBoolAnd
                 | ExprCmpBoolEq

ExprOpBoolOr    := ExprOpBoolOr OpBoolOr OptNewline ExprOpBoolOr
                 | ExprOpBoolAnd

Expression      := ExprOpBoolOr

Assignment      := Lval (Assign) OptNewline Expression

Block           := (OpenBrace) StatementList (CloseBrace)

IfStatement     := (If) Expression OptNewline Block

ElseIfStatement := (ElseIf) Expression OptNewline Block

ElseStatement   := (Else) OptNewline Block

IfChain         := IfStatement IfChainPrime

IfChainPrime    := ElseIfStatement IfChainPrime
                 | ElseStatement
                 | Epsilon

ImportStatement := (Import) (Identifier)

FnParamDefs     := (Identifier) TypeAnnotation FnPmDefsPrime
                 | (Identifier) FnPmDefsPrime
                 | Epsilon

FnPmDefsPrime   := (Comma) OptNewline (Identifier) TypeAnnotation FnPmDefsPrime
                 | (Comma) OptNewline (Identifier) FnPmDefsPrime
                 | Epsilon

FnReturnType    := (RightArrow) Type
                 | Epsilon

FunctionSig     := (FunctionDefKeyword) (Identifier) (OpenParen) OptNewline FnParamDefs OptNewline (CloseParen) OptNewline FnReturnType

FunctionDef     := FunctionSig OptNewline Block

ClassField      := (Identifier) TypeAnnotation (Assign) (Expression)
                 | (Identifier) TypeAnnotation

ClassFields     := ClassField (Newline) ClassFldsPrime
                 | Epsilon

ClassFldsPrime  := ClassField (Newline) ClassFldsPrime
                 | Epsilon

ClassFunctions  := FunctionDef (Newline) ClassFnsPrime
                 | Epsilon

ClassFnsPrime   := FunctionDef (Newline) ClassFnsPrime
                 | Epsilon

ClassDef        := (ClassDefKeyword) (Identifier) OptNewline (OpenBrace) (Newline) ClassFields ClassFunctions (CloseBrace)

ReturnStatement := (Return) Expression
                 | (Return)

AnnotationBody  := (Identifier)
                 | Literal

Annotation      := (AnnotationBegin) AnnotationBody (AnnotationEnd)

Annotatable     := FunctionDef

StatementEnd    := (Newline)

StatementNaught := FunctionDef
                 | ClassDef
                 | ImportStatement
                 | FnInvocation
                 | ReturnStatement
                 | Assignment
                 | VarDecl
                 | ConstDecl
                 | IfChain
                 | Block
                 | Annotation StatementEnd Annotatable

Statement       := StatementNaught StatementEnd

StatementList   := Statement StatementList
                 | StatementEnd StatementList
                 | Epsilon

Program         := StatementList
